set dotenv-load

editor := 'nvim'
today := `date +"%d" | bc`
year := file_stem(justfile_directory())

day day=today:
  #!/usr/bin/env bash
  set -e

  D=./Day{{day}}
  I=./{{day}}.in
  if [ "$EX" = "1" ]; then I=ex.in; fi

  if [ -f $D.rs ]; then
    cargo build --release --bin Day{{day}}
    cmd=target/release/Day{{day}}
  elif [ -f $D.c ]; then
    gcc -std=c11 -O3 $D.c
    cmd=./a.out
  elif [ -f $D.cpp ]; then
    g++ -std=c++23 -O3 $D.cpp
    cmd=./a.out
  else
    cmd=$D.*
  fi

  res=$($cmd < $I | tee /dev/tty | grep -e "Part[12]:")
  p1=$(echo "$res" | yq .Part1)
  p2=$(echo "$res" | yq .Part2)

  echo
  if ! [ $p2 = "null" ]; then
    echo -n $p2 | wl-copy
    echo Copied \'$p2\'
  else echo $p1
    echo -n $p1 | wl-copy
    echo Copied \'$p1\'
  fi

bench day=today:
  #!/usr/bin/env bash
  set -e

  D=./Day{{day}}
  I=./{{day}}.in

  if [ -f $D.rs ]; then
    cargo build --release --bin Day{{day}}
    cmd=target/release/Day{{day}}
  elif [ -f $D.c ]; then
    gcc -std=c11 -O3 $D.c
    cmd=./a.out
  elif [ -f $D.cpp ]; then
    g++ -std=c++23 -O3 $D.cpp
    cmd=./a.out
  else
    cmd=$D.*
  fi
  
  bench_cmd="hyperfine --warmup 20 --input $I $cmd"
  echo Î» "$bench_cmd"
  $bench_cmd

watch day=today:
  find . -maxdepth 1 -type f -name Day\*.\* -or -name \*.in | entr -rcs 'just day {{day}}'

_gen template day fname:
  cp "template.{{template}}" {{fname}}
  sed -i "s/_DAY_/{{day}}/g" {{fname}}
  chmod +x {{fname}}

gen template day=today: (_gen (template) (day) ('Day' + day + '.' + template))

getinput day=today:
  # .env must contain session cookie in variable SESSION
  @curl -Ssl \
    -A "github.com/vypxl/aoc: {{year}}/getinput by thomas@vypxl.io" \
    -H "cookie: session=$SESSION" \
    https://adventofcode.com/{{year}}/day/{{day}}/input > {{day}}.in

export TZ := "UTC"
schedulegetinput:
  echo "sleep 2; just --justfile {{justfile()}} getinput" | at 5am

submit day part answer:
  # cookiefile must contain session cookie
  @curl -Ssl "https://adventofcode.com/{{year}}/day/{{day}}/answer" \
    -A "github.com/vypxl/aoc: {{year}}/submit by thomas@vypxl.io" \
    -H "cookie: session=$SESSION" \
    -H 'content-type: application/x-www-form-urlencoded' \
    -H 'accept: text/html,application/xhtml+xml,application/xml;q=0.9,image/avif,image/webp,image/apng,*/*;q=0.8,application/signed-exchange;v=b3;q=0.9' \
    --data-raw "level={{part}}&answer={{answer}}" \
    | lynx -stdin -dump -nolist | awk -v RS= 'END{if (NR) print}'

viewinput day=today:
  cat {{day}}.in

edit day=today:
  {{editor}} Day{{day}}.* {{day}}.in

setup template="py" day=today: (gen (template) (day)) (getinput (day)) schedulegetinput (watch (day))
